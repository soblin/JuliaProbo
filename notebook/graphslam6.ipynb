{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "about-operation",
   "metadata": {
    "scrolled": false
   },
   "outputs": [],
   "source": [
    "using Pkg\n",
    "if isfile(\"../Project.toml\") && isfile(\"../Manifest.toml\")\n",
    "    Pkg.activate(\"..\");\n",
    "    ENV[\"PYTHON\"] = \"python3\";\n",
    "end\n",
    "\n",
    "using Plots\n",
    "using LinearAlgebra\n",
    "import Plots: Plot\n",
    "using JuliaProbo\n",
    "gr();"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "gross-apparatus",
   "metadata": {
    "scrolled": false
   },
   "outputs": [],
   "source": [
    "using Plots, Random, Distributions, LinearAlgebra, JuliaProbo\n",
    "import Plots: Plot\n",
    "gr();\n",
    "\n",
    "struct ObsEdge\n",
    "    t1::Int64\n",
    "    t2::Int64\n",
    "    x1::Vector{Float64}\n",
    "    x2::Vector{Float64}\n",
    "    z1::Vector{Float64}\n",
    "    z2::Vector{Float64}\n",
    "    ω_ul::Matrix{Float64}\n",
    "    ω_ur::Matrix{Float64}\n",
    "    ω_ll::Matrix{Float64}\n",
    "    ω_lr::Matrix{Float64}\n",
    "    xi_u::Vector{Float64}\n",
    "    xi_l::Vector{Float64}\n",
    "    function ObsEdge(\n",
    "        t1::Int64,\n",
    "        t2::Int64,\n",
    "        z1_::Vector{Float64},\n",
    "        z2_::Vector{Float64},\n",
    "        xs::Dict{Int64, Vector{Float64}},\n",
    "        sensor_noise_rate=[0.14, 0.05, 0.05]\n",
    "    )\n",
    "        z1_[1] == z2_[1] || error(\"Landmark Ids do not match\")\n",
    "        x1 = copy(xs[t1])\n",
    "        x2 = copy(xs[t2])\n",
    "        z1 = z1_[2:4]\n",
    "        z2 = z2_[2:4]\n",
    "        θ1 = x1[3]\n",
    "        ϕ1 = z1[2]\n",
    "        θ2 = x2[3]\n",
    "        ϕ2 = z2[2]\n",
    "        s1 = sin(θ1 + ϕ1)\n",
    "        c1 = cos(θ1 + ϕ1)\n",
    "        s2 = sin(θ2 + ϕ2)\n",
    "        c2 = cos(θ2 + ϕ2)\n",
    "        \n",
    "        ê = [z2[1] * c2 - z1[1] * c1,\n",
    "             z2[1] * s2 - z1[1] * s1,\n",
    "             z2[2] - z2[3] - z1[2] + z1[3]]\n",
    "        ê = ê + x2 - x1\n",
    "        while ê[3] >= pi\n",
    "            ê[3] -= 2*pi\n",
    "        end\n",
    "        while ê[3] < -pi\n",
    "            ê[3] += 2*pi\n",
    "        end\n",
    "        \n",
    "        Q₁ = Diagonal( [(z1[1]*sensor_noise_rate[1])^2, sensor_noise_rate[2]^2, sensor_noise_rate[3]^2] )\n",
    "        R₁ = -[ c1 (-z1[1]*s1) 0; s1 (z1[1]*c1) 0; 0 1 -1]\n",
    "        Q₂ = Diagonal( [(z2[1]*sensor_noise_rate[1])^2, sensor_noise_rate[2]^2, sensor_noise_rate[3]^2] )\n",
    "        R₂ = [ c2 (-z2[1]*s2) 0; s2 (z2[1]*c2) 0; 0 1 -1 ]\n",
    "        Σ = R₁ * Q₁ * transpose(R₁) + R₂ * Q₂ * transpose(R₂)\n",
    "        Ω = inv(Σ)\n",
    "        B1 = -[ 1 0 -z1[1]*s1; 0 1 z1[1]*c1; 0 0 1]\n",
    "        B2 = [ 1 0 -z2[1]*s2; 0 1 z2[1]*c2; 0 0 1]\n",
    "        ω_ul = transpose(B1) * Ω * B1\n",
    "        ω_ur = transpose(B1) * Ω * B2\n",
    "        ω_ll = transpose(B2) * Ω * B1\n",
    "        ω_lr = transpose(B2) * Ω * B2\n",
    "        xi_u = -transpose(B1) * Ω * ê\n",
    "        xi_l = -transpose(B2) * Ω * ê\n",
    "        # println(ê)\n",
    "        # println(Σ)\n",
    "        new(t1, t2, x1, x2, z1, z2, ω_ul, ω_ur, ω_ll, ω_lr, xi_u, xi_l)\n",
    "    end\n",
    "end\n",
    "\n",
    "mutable struct MotionEdge\n",
    "    t1::Int64\n",
    "    t2::Int64\n",
    "    x̂₁::Vector{Float64}\n",
    "    x̂₂::Vector{Float64}\n",
    "    ω_ul::Matrix{Float64}\n",
    "    ω_ur::Matrix{Float64}\n",
    "    ω_ll::Matrix{Float64}\n",
    "    ω_lr::Matrix{Float64}\n",
    "    xi_u::Vector{Float64}\n",
    "    xi_l::Vector{Float64}\n",
    "end\n",
    "\n",
    "function make_ax(xlim, ylim)\n",
    "    p = plot(aspect_ratio=:equal, xlim=xlim, ylim=ylim, xlabel=\"X\", ylabel=\"Y\")\n",
    "    return p\n",
    "end\n",
    "\n",
    "function draw_traj!(xs, p::Plot{T}) where {T}\n",
    "    # xs is dictionary\n",
    "    poses = [xs[s] for s in 0:length(xs)-1]\n",
    "    p = scatter!([e[1] for e in poses], [e[2] for e in poses], markershape=:circle, markersize=1, color=\"black\")\n",
    "    p = plot!([e[1] for e in poses], [e[2] for e in poses], linewidth=0.5, color=\"black\")\n",
    "end\n",
    "\n",
    "function draw_observations!(\n",
    "        xs::Dict{Int64, Vector{Float64}},\n",
    "        zlist::Dict{Int64, Vector{Vector{Float64}}},\n",
    "        p::Plot{T}\n",
    "        ) where {T}\n",
    "    for s in 0:length(xs)-1\n",
    "        if !haskey(zlist, s)\n",
    "            continue\n",
    "        end\n",
    "        v = zlist[s]\n",
    "        N = size(v)[1]\n",
    "        for i in 1:N\n",
    "            x, y, θ = xs[s][1], xs[s][2], xs[s][3]\n",
    "            obsv = v[i]\n",
    "            d, ϕ = obsv[2], obsv[3]\n",
    "            mx = x + d * cos(θ + ϕ)\n",
    "            my = y + d * sin(θ + ϕ)\n",
    "            p = plot!([x, mx], [y, my], color=\"pink\", alpha=0.5)\n",
    "        end\n",
    "    end\n",
    "end\n",
    "\n",
    "function draw_edges!(edges::Vector{ObsEdge}, p::Plot{T}) where {T}\n",
    "    for edge = edges\n",
    "        p = plot!([edge.x1[1], edge.x2[1]], [edge.x1[2], edge.x2[2]], color=\"red\", alpha=0.5)\n",
    "    end\n",
    "end\n",
    "\n",
    "function draw_all(\n",
    "    xlim, ylim,\n",
    "    xs::Dict{Int64, Vector{Float64}},\n",
    "    zlist::Dict{Int64, Vector{Vector{Float64}}},\n",
    "    edges::Vector{ObsEdge},\n",
    "    p::Plot{T}\n",
    ") where {T}\n",
    "    p = make_ax(xlim, ylim)\n",
    "    draw_observations!(xs, zlist, p)\n",
    "    draw_traj!(xs, p)\n",
    "    # draw_edges!(edges, p)\n",
    "    return p\n",
    "end\n",
    "\n",
    "function read_data(fname=\"log.txt\")\n",
    "    hat_xs = Dict{Int64, Vector{Float64}}()\n",
    "    zlist = Dict{Int64, Vector{Vector{Float64}}}()\n",
    "    fd = open(fname, \"r\")\n",
    "    lines = readlines(fd)\n",
    "    for i in 1:length(lines)\n",
    "        line = lines[i]\n",
    "        tokens = split(line, ' ')\n",
    "        step = parse(Int64, tokens[2])\n",
    "        if tokens[1] == \"x\"\n",
    "            # x, y, θ\n",
    "            hat_xs[step] = [parse(Float64, tokens[3]), parse(Float64, tokens[4]), parse(Float64, tokens[5])] \n",
    "        elseif tokens[1] == \"z\"\n",
    "            if !haskey(zlist, step)\n",
    "                zlist[step] = Vector{Vector{Float64}}(undef, 0)\n",
    "            end\n",
    "            # ID of landmark, d, ϕ, m\n",
    "            z = [parse(Float64, tokens[3]), parse(Float64, tokens[4]), parse(Float64, tokens[5]), parse(Float64, tokens[6])]\n",
    "           push!(zlist[step], z)\n",
    "        end\n",
    "    end\n",
    "    return hat_xs, zlist\n",
    "end\n",
    "\n",
    "function make_edges(\n",
    "    hat_xs::Dict{Int64, Vector{Float64}},\n",
    "    zlist::Dict{Int64, Vector{Vector{Float64}}}\n",
    ")\n",
    "    landmark_keys_zlist = Dict{Int64, Vector{Vector{Float64}}}()\n",
    "    \n",
    "    for (step, zs) in zlist\n",
    "        for i in 1:lastindex(zs)\n",
    "            z = zs[i]\n",
    "            landmark_id = convert(Int64, z[1])\n",
    "            if !haskey(landmark_keys_zlist, landmark_id)\n",
    "                landmark_keys_zlist[landmark_id] = Vector{Vector{Float64}}(undef, 0)\n",
    "            end\n",
    "            # step, ID of landmark, d, ϕ, m\n",
    "            push!(landmark_keys_zlist[landmark_id], vcat([step*1.0], z))\n",
    "        end\n",
    "    end\n",
    "\n",
    "    edges = Vector{ObsEdge}(undef, 0)\n",
    "    \n",
    "    for (landmark_id, lm_zlist) in landmark_keys_zlist\n",
    "        for i in 1:lastindex(lm_zlist)\n",
    "            for j in (i+1):lastindex(lm_zlist)\n",
    "                z1 = lm_zlist[i]\n",
    "                z2 = lm_zlist[j]\n",
    "                t1 = convert(Int64, z1[1])\n",
    "                t2 = convert(Int64, z2[1])\n",
    "                push!(edges, ObsEdge(t1, t2, z1[2:5], z2[2:5], hat_xs))\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "\n",
    "    return edges\n",
    "end\n",
    "\n",
    "function add_edge!(edge::ObsEdge, Ω::Matrix{Float64}, xi::Vector{Float64})\n",
    "    f1, f2 = edge.t1*3+1, edge.t2*3+1\n",
    "    t1, t2 = f1+2, f2+2\n",
    "    Ω[f1:t1, f1:t1] += edge.ω_ul\n",
    "    Ω[f1:t1, f2:t2] += edge.ω_ur\n",
    "    Ω[f2:t2, f1:t1] += edge.ω_ll\n",
    "    Ω[f2:t2, f2:t2] += edge.ω_lr\n",
    "    xi[f1:t1] += edge.xi_u\n",
    "    xi[f2:t2] += edge.xi_l\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "promotional-color",
   "metadata": {},
   "outputs": [],
   "source": [
    "hat_xs, zlist = read_data(\"log_official.txt\");\n",
    "dim = length(hat_xs) * 3 # dimension of trajectory\n",
    "for cnt in 1:1000\n",
    "    edges = make_edges(hat_xs, zlist)\n",
    "    Ω = zeros(Float64, dim, dim)\n",
    "    xi = zeros(Float64, dim)\n",
    "    Ω[1:3, 1:3] += Matrix(10000000.0I, 3, 3)\n",
    "    \n",
    "    for edge = edges\n",
    "        add_edge!(edge, Ω, xi)\n",
    "    end\n",
    "    \n",
    "    Δxs = inv(Ω) * xi\n",
    "    \n",
    "    for i in 0:length(hat_xs)-1\n",
    "        hat_xs[i] += Δxs[i*3+1:(i+1)*3]\n",
    "    end\n",
    "    \n",
    "    diff = norm(Δxs)\n",
    "    println(\"$(cnt)-th iteration: ϵ = $(diff)\")\n",
    "    if diff < 0.01\n",
    "        #draw(hat_xs, zlist, edges)\n",
    "        break\n",
    "    end\n",
    "end\n",
    "xlim = [-5.0, 5.0]\n",
    "ylim = [-5.0, 5.0]\n",
    "edges = make_edges(hat_xs, zlist)\n",
    "p = make_ax(xlim, ylim)\n",
    "p = draw_all(xlim, ylim, hat_xs, zlist, edges, p)\n",
    "plot(p, legend=nothing)\n",
    "# savefig(p, \"images/ch09_graphslam5.png\")"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "monthly-albuquerque",
   "metadata": {},
   "source": [
    "<img src=\"images/ch09_graphslam5.png\">"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.5.3",
   "language": "julia",
   "name": "julia-1.5"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.5.3"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
